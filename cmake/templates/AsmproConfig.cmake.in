# ===================================================================================
#   Asmpro CMake 配置文件
#
#               ** 此文件自动生成，请勿修改 **
#
#   外部项目使用说明:
#       在你的 CMakeLists.txt 中，添加以下语句:
#       
#       find_package(Asmpro REQUIRED)
#       target_include_directories(MY_TARGET_NAME PUBLIC ${Asmpro_INCLUDE_DIRS})
#       target_link_libraries(MY_TARGET_NAME ${Asmpro_LIBS})
#
#       此文件将定义以下变量:
#         - Asmpro_LIBS           : Asmpro 模块的所有导入目标的列表
#         - Asmpro_INCLUDE_DIRS   : Asmpro 包含目录
#         - Asmpro_FOUND          : Asmpro 是否被寻找到的标志位
#
#       高级变量:
#         - Asmpro_INSTALL_PATH   : Asmpro 被安装到的位置
#         - Asmpro_LIB_COMPONENTS : 给出的 Asmpro 模块列表
#
# ===================================================================================

set(Asmpro_LIB_COMPONENTS @Asmpro_MODULES_CONFIGCMAKE@)
set(__Asmpro_INCLUDE_DIRS @Asmpro_INCLUDE_DIRS_CONFIGCMAKE@)

include(FindPackageHandleStandardArgs)

# 获取没有 ../.. 相对路径标记的绝对路径，消除隐式链接器警告
get_filename_component(Asmpro_CONFIG_PATH "${CMAKE_CURRENT_LIST_DIR}" REALPATH)
get_filename_component(Asmpro_INSTALL_PATH "${Asmpro_CONFIG_PATH}/@Asmpro_INSTALL_PATH_RELATIVE_CONFIGCMAKE@" REALPATH)

if(NOT OpenCV_FOUND)
    find_package(OpenCV REQUIRED)
endif(NOT OpenCV_FOUND)
if(NOT open62541_FOUND)
    find_package(open62541 REQUIRED)
endif(NOT open62541_FOUND)

# 头文件安装配置
set(Asmpro_INCLUDE_DIRS "")
foreach(d ${__Asmpro_INCLUDE_DIRS})
    get_filename_component(__d "${d}" REALPATH)
    if(NOT EXISTS "${__d}")
        message(WARNING "Asmpro: Include directory doesn't exist: '${d}'. Asmpro installation may be broken. Skip...")
    else()
        list(APPEND Asmpro_INCLUDE_DIRS "${__d}")
    endif()
endforeach()
unset(__d)

if(NOT TARGET asmpro_feature)
    include(${CMAKE_CURRENT_LIST_DIR}/AsmproTargets.cmake)
endif()

# 库文件安装配置
foreach(__asmprocomponent ${Asmpro_LIB_COMPONENTS})
    if(NOT __asmprocomponent MATCHES "^asmpro_")
        set(__asmprocomponent asmpro_${__asmprocomponent})
    endif()
    set(Asmpro_LIBS ${Asmpro_LIBS} "${__asmprocomponent}")
endforeach()



# We do not actually need REQUIRED_VARS to be checked for. Just use the
# installation directory for the status.
find_package_handle_standard_args(
    Asmpro
    REQUIRED_VARS Asmpro_INSTALL_PATH
)
